apply plugin: 'groovy'
apply plugin: 'eclipse'

version = '0.1.1'
sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    compile     'org.codehaus.groovy:groovy-all:2.5.8'
    testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'
}

jar {
    archiveName = 'kin.jar'
    manifest {
        attributes 'Main-Class': 'hr.helix.kin.Main'
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    from 'LICENSE'
}

task dist(type: Zip) {
    dependsOn jar, groovydoc
    from jar.archivePath
    from 'LICENSE'
    from 'README.md'
    into('docs/api') {
        from "$buildDir/docs/groovydoc"
    }
    into('src') {
        from 'src/main/groovy'
    }
}

build.dependsOn dist
