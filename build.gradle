apply plugin: 'groovy'
apply plugin: 'eclipse'

version = 0.1
sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    groovy      'org.codehaus.groovy:groovy-all:1.8.7'
    testCompile 'org.spockframework:spock-core:0.6-groovy-1.8'
}

jar {
    archiveName = 'kin.jar'
    manifest {
        attributes 'Main-Class': 'hr.helix.kin.Main'
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    from 'LICENSE'
}

task dist(type: Zip) {
    dependsOn jar, groovydoc
    from jar.archivePath
    from 'LICENSE'
    from 'README.md'
    into('docs/api') {
        from "$buildDir/docs/groovydoc"
    }
    into('src') {
        from 'src/main/groovy'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.1'
}
